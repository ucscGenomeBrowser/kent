// A simple ascii game of hangman.

include lib

array of string words = 
   (
   "spider",
   "investigate",
   "wonder",
   "library",
   "sparrow",
   "fiction",
   "experience",
   "flowers",
   "beetle",
   "muffin",
   "television",
   "refrigerator",
   "waterfall",
   "museum",
   "butterfly",
   "juice",
   "computer",
   "picture",
   "astronomy",
   "charcoal",
   "springtime",
   "archive",
   "musical",
   "jewelry",
   "flight",
   "comfortable",
   "kangaroo",
   "willow",
   "melody",
   "underneath",
   "related",
   );

/* Pick our word randomly from list. */
randInit();
string word = words[lib.randomIx(words.size)];
// This is a comment.
int maxWrong = 11;

/* Print out instructions. */
print("Let's play hangman. You can get " + maxWrong + " letters wrong:");

/* Keep track of the parts of word we've guessed and # of guesses. */
array[word.size] of bit guessed;
int wrongCount = 0;
string guesses = "";

/* The main game loop.   */
for (;;)
   {
   /* Create a string that is dashes where we haven't guessed the
    * word yet. */
   string s = word.dupe();
   for (int i=0; i<word.size; i += 1)
       {
       if (!guessed[i])
	   s[i] = '-';
       }

   /* Print out guess count and what we know of word. */
   print(wrongCount + " " + s + " (" + guesses + ")");

   /* Wait for keypress, and convert key to lower case. */
   string key = keyIn();
   key.lower();
   guesses += key;

   /* Mark spot on words that use this letter, and count
    * up all the letters in the word that we know. */
   int goodLetters = 0;
   for (int i=0; i<word.size; i += 1)
       {
       if (word[i] == key[0])
	  guessed[i] = 1;
       if (guessed[i])
          goodLetters += 1;
       }

   /* Hey, we've got all the letters, we're done. */
   if (goodLetters == word.size)
       {
       print(word + "!!!  You won.");
       break;
       }

    /* See if we've run out of wrong guesses. */
    if (word.find(key) < 0)
        {
	wrongCount += 1;
	if (wrongCount >= maxWrong)
	    {
	    print("Sorry, after " + maxWrong + " wrong guesses you are hung on "
		   + word + "!");
	    break;
	    }
	}
    }
